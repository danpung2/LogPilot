plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.graalvm.buildtools.native'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.logpilot'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    implementation project(':logpilot-core')

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.bucket4j:bucket4j-core:8.10.1'

    // Micrometer Prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // gRPC
    implementation 'net.devh:grpc-spring-boot-starter:3.1.0.RELEASE'
    implementation 'io.grpc:grpc-protobuf:1.63.0'
    implementation 'io.grpc:grpc-stub:1.63.0'

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.24.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.63.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

graalvmNative {
    testSupport = false
}

tasks.named('processTestAot') {
    enabled = false
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
// Workaround for IDEs complaining about missing AOT generated source folders
// These folders are registered by the protobuf/native plugins but not created unless AOT tasks run.
['aot', 'aotTest'].each { name ->
    ['java', 'grpc'].each { type ->
        def dir = file("build/generated/source/proto/${name}/${type}")
        if (!dir.exists()) {
            dir.mkdirs()
        }
    }
}
