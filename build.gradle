plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'org.graalvm.buildtools.native' version '0.10.2' apply false
    id 'com.google.protobuf' version '0.9.4' apply false
}

allprojects {
    group = 'io.github.danpung2'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:3.3.4'
        }
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
    }
}

configure(subprojects.findAll { it.name in ['logpilot-core', 'logpilot-client', 'logpilot-spring-boot-starter', 'logpilot-logback'] }) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                
                pom {
                    name = project.name
                    description = 'LogPilot: A lightweight log collection system'
                    url = 'https://github.com/danpung2/LogPilot'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'danpung2'
                            name = 'danpung2'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/danpung2/LogPilot.git'
                        developerConnection = 'scm:git:ssh://github.com/danpung2/LogPilot.git'
                        url = 'https://github.com/danpung2/LogPilot'
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'OSSRH'
                url = version.endsWith('SNAPSHOT') 
                    ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/' 
                    : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username = project.findProperty('ossrhUsername')
                    password = project.findProperty('ossrhPassword')
                }
            }
        }
    }

    signing {
        required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}